// SPDX-License-Identifier: MIT
pragma solidity ^0.8.22;

import { Initializable } from "@openzeppelin/contracts-upgradeable/proxy/utils/Initializable.sol";
import { UUPSUpgradeable } from "@openzeppelin/contracts-upgradeable/proxy/utils/UUPSUpgradeable.sol";
import { OwnableUpgradeable } from "@openzeppelin/contracts-upgradeable/access/OwnableUpgradeable.sol";
import { ReentrancyGuardUpgradeable } from "@openzeppelin/contracts-upgradeable/utils/ReentrancyGuardUpgradeable.sol";
import "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import "@openzeppelin/contracts/token/ERC20/utils/SafeERC20.sol";

// Interface for OpenworkGenesis storage contract
interface IOpenworkGenesis {
    enum JobStatus { Open, InProgress, Completed, Cancelled }
    
    struct Profile {
        address userAddress;
        string ipfsHash;
        address referrerAddress;
        string[] portfolioHashes;
    }
    
    struct MilestonePayment {
        string descriptionHash;
        uint256 amount;
    }
    
    struct Application {
        uint256 id;
        string jobId;
        address applicant;
        string applicationHash;
        MilestonePayment[] proposedMilestones;
        uint32 preferredPaymentChainDomain;
        address preferredPaymentAddress;
    }
    
    struct Job {
        string id;
        address jobGiver;
        address[] applicants;
        string jobDetailHash;
        JobStatus status;
        string[] workSubmissions;
        MilestonePayment[] milestonePayments;
        MilestonePayment[] finalMilestones;
        uint256 totalPaid;
        uint256 currentMilestone;
        address selectedApplicant;
        uint256 selectedApplicationId;
        uint32 paymentTargetChainDomain;
        address paymentTargetAddress;
        uint32 applierOriginChainDomain;
    }

    function setProfile(address user, string memory ipfsHash, address referrer) external;
    function addPortfolio(address user, string memory portfolioHash) external;
    function setJob(string memory jobId, address jobGiver, string memory jobDetailHash, string[] memory descriptions, uint256[] memory amounts) external;
    function addJobApplicant(string memory jobId, address applicant) external;
    function setJobApplication(string memory jobId, uint256 applicationId, address applicant, string memory applicationHash, string[] memory descriptions, uint256[] memory amounts, uint32 preferredPaymentChainDomain, address preferredPaymentAddress) external;
    function updateJobStatus(string memory jobId, JobStatus status) external;
    function setJobSelectedApplicant(string memory jobId, address applicant, uint256 applicationId) external;
    function setJobCurrentMilestone(string memory jobId, uint256 milestone) external;
    function addJobFinalMilestone(string memory jobId, string memory description, uint256 amount) external;
    function addWorkSubmission(string memory jobId, string memory submissionHash) external;
    function updateJobTotalPaid(string memory jobId, uint256 amount) external;
    function setJobRating(string memory jobId, address user, uint256 rating) external;
    function setJobPaymentTarget(string memory jobId, uint32 targetChainDomain, address targetAddress, uint32 applierOriginChainDomain) external;
    function setApplicationPaymentPreference(string memory jobId, uint256 applicationId, uint32 preferredChainDomain, address preferredAddress) external;
    function setUserTotalOWTokens(address user, uint256 tokens) external;
    function incrementUserGovernanceActions(address user) external;
    function setUserGovernanceActions(address user, uint256 actions) external;
    function updateUserClaimData(address user, uint256 claimedTokens) external;    
    function getProfile(address user) external view returns (Profile memory);
    function getJob(string memory jobId) external view returns (Job memory);
    function getJobApplication(string memory jobId, uint256 applicationId) external view returns (Application memory);
    function getJobCount() external view returns (uint256);
    function getAllJobIds() external view returns (string[] memory);
    function getJobsByPoster(address poster) external view returns (string[] memory);
    function getJobApplicationCount(string memory jobId) external view returns (uint256);
    function getUserRatings(address user) external view returns (uint256[] memory);
    function jobExists(string memory jobId) external view returns (bool);
    function hasProfile(address user) external view returns (bool);
    function getUserReferrer(address user) external view returns (address);
    function getUserEarnedTokens(address user) external view returns (uint256);
    function getUserGovernanceActionsInBand(address user, uint256 band) external view returns (uint256);
    function getUserGovernanceActions(address user) external view returns (uint256);
    function getUserRewardInfo(address user) external view returns (uint256 totalTokens, uint256 governanceActions);
    function totalPlatformPayments() external view returns (uint256);
}

interface IOpenWorkRewards {
    function processJobPayment(address jobGiver, address jobTaker, uint256 amount, uint256 newPlatformTotal) external returns (uint256[] memory tokensAwarded);
    function recordGovernanceAction(address user) external;
    function calculateUserClaimableTokens(address user) external view returns (uint256);
    function claimTokens(address user, uint256 amount) external returns (bool);
    function getUserTotalTokensEarned(address user) external view returns (uint256);
    function getUserGovernanceActionsInBand(address user, uint256 band) external view returns (uint256);
    function getUserTotalGovernanceActions(address user) external view returns (uint256);
    function calculateTokensForRange(uint256 fromAmount, uint256 toAmount) external view returns (uint256);
    function getCurrentBand() external view returns (uint256);
    function getPlatformBandInfo() external view returns (uint256 currentBand, uint256 currentTotal, uint256 bandMinAmount, uint256 bandMaxAmount, uint256 governanceRewardRate);
    function getUserTotalClaimableTokens(address user) external view returns (uint256);
    function markTokensClaimed(address user, uint256 amount) external returns (bool);
}

interface INativeBridge {
    function sendSyncRewardsData(address user, uint256 claimableAmount, bytes calldata _options) external payable;
    function sendSyncVotingPower(address user, uint256 totalRewards, bytes calldata _options) external payable;
}

contract NativeOpenWorkJobContract is 
    Initializable,
    UUPSUpgradeable,
    OwnableUpgradeable,
    ReentrancyGuardUpgradeable
{
    using SafeERC20 for IERC20;

    // Reuse interface enums and structs instead of duplicating
    enum JobStatus { Open, InProgress, Completed, Cancelled }
    
    // ==================== STATE VARIABLES ====================
    
    IOpenworkGenesis public genesis;
    IOpenWorkRewards public rewardsContract;
    address public bridge;
    address[] private allProfileUsers;
    uint256 private profileCount;
    mapping(address => bool) public authorizedContracts;
    IERC20 public usdtToken;
    address public cctpReceiver;
    address public cctpTransceiver;
    mapping(uint32 => address) public chainDomainToCCTPReceiver;
    uint32 public currentChainCCTPDomain;

    // ==================== EVENTS ====================
    
    event ProfileCreated(address indexed user, string ipfsHash, address referrer);
    event JobPosted(string indexed jobId, address indexed jobGiver, string jobDetailHash);
    event JobApplication(string indexed jobId, uint256 indexed applicationId, address indexed applicant, string applicationHash);
    event JobStarted(string indexed jobId, uint256 indexed applicationId, address indexed selectedApplicant, bool useApplicantMilestones);
    event WorkSubmitted(string indexed jobId, address indexed applicant, string submissionHash, uint256 milestone);
    event PaymentReleased(string indexed jobId, address indexed jobGiver, address indexed applicant, uint256 amount, uint256 milestone);
    event MilestoneLocked(string indexed jobId, uint256 newMilestone, uint256 lockedAmount);
    event UserRated(string indexed jobId, address indexed rater, address indexed rated, uint256 rating);
    event PortfolioAdded(address indexed user, string portfolioHash);
    event JobStatusChanged(string indexed jobId, JobStatus newStatus);
    event PaymentReleasedAndNextMilestoneLocked(string indexed jobId, uint256 releasedAmount, uint256 lockedAmount, uint256 milestone);
    event BridgeUpdated(address indexed oldBridge, address indexed newBridge);
    event GenesisUpdated(address indexed oldGenesis, address indexed newGenesis);
    event RewardsContractUpdated(address indexed oldRewards, address indexed newRewards);
    event GovernanceActionIncremented(address indexed user, uint256 newGovernanceActionCount, uint256 indexed band);
    event TokensEarned(address indexed user, uint256 tokensEarned, uint256 newPlatformTotal, uint256 newUserTotalTokens);
    event ClaimDataUpdated(address indexed user, uint256 claimedJobTokens, uint256 claimedGovernanceTokens);
    event RewardsDataSynced(address indexed user, uint256 syncType, uint256 claimableAmount, uint256 reserved);   
    event AuthorizedContractAdded(address indexed contractAddress);
    event AuthorizedContractRemoved(address indexed contractAddress);

    /// @custom:oz-upgrades-unsafe-allow constructor
    constructor() {
        _disableInitializers();
    }

    function initialize(
        address _owner, 
        address _bridge, 
        address _genesis,
        address _rewardsContract,
        address _usdtToken,
        address _cctpReceiver
    ) public initializer {
        __Ownable_init(_owner);
        __UUPSUpgradeable_init();
        __ReentrancyGuard_init();
        
        bridge = _bridge;
        genesis = IOpenworkGenesis(_genesis);
        rewardsContract = IOpenWorkRewards(_rewardsContract);
        usdtToken = IERC20(_usdtToken);
        cctpReceiver = _cctpReceiver;
    }

    function _authorizeUpgrade(address newImplementation) internal override onlyOwner {}

    function addAuthorizedContract(address contractAddress) external onlyOwner {
        require(contractAddress != address(0), "Zero address");
        authorizedContracts[contractAddress] = true;
        emit AuthorizedContractAdded(contractAddress);
    }

    function removeAuthorizedContract(address contractAddress) external onlyOwner {
        authorizedContracts[contractAddress] = false;
        emit AuthorizedContractRemoved(contractAddress);
    }

    function isAuthorizedContract(address contractAddress) external view returns (bool) {
        return authorizedContracts[contractAddress];
    }

    // ==================== ADMIN FUNCTIONS ====================
    
    function setBridge(address _bridge) external onlyOwner {
        address oldBridge = bridge;
        bridge = _bridge;
        emit BridgeUpdated(oldBridge, _bridge);
    }
    
    function setGenesis(address _genesis) external onlyOwner {
        address oldGenesis = address(genesis);
        genesis = IOpenworkGenesis(_genesis);
        emit GenesisUpdated(oldGenesis, _genesis);
    }
    
    function setRewardsContract(address _rewardsContract) external onlyOwner {
        address oldRewards = address(rewardsContract);
        rewardsContract = IOpenWorkRewards(_rewardsContract);
        emit RewardsContractUpdated(oldRewards, _rewardsContract);
    }
    
    function setCCTPReceiver(address _cctpReceiver) external onlyOwner {
        require(_cctpReceiver != address(0), "Zero address");
        cctpReceiver = _cctpReceiver;
    }
    
    function setCCTPTransceiver(address _transceiver) external onlyOwner {
        require(_transceiver != address(0), "Zero address");
        cctpTransceiver = _transceiver;
    }
    
    function setCurrentChainCCTPDomain(uint32 _domain) external onlyOwner {
        require(_domain > 0, "Invalid domain");
        currentChainCCTPDomain = _domain;
    }
    
    function setChainCCTPReceiver(uint32 _domain, address _receiver) external onlyOwner {
        require(_domain > 0, "Invalid domain");
        require(_receiver != address(0), "Zero address");
        chainDomainToCCTPReceiver[_domain] = _receiver;
    }
    
    function setMultipleChainCCTPReceivers(uint32[] memory _domains, address[] memory _receivers) external onlyOwner {
        require(_domains.length == _receivers.length, "Length mismatch");
        for (uint i = 0; i < _domains.length; i++) {
            require(_domains[i] > 0, "Invalid domain");
            require(_receivers[i] != address(0), "Zero address");
            chainDomainToCCTPReceiver[_domains[i]] = _receivers[i];
        }
    }
    
    function withdrawFunds(address _to, uint256 _amount) internal {
        require(cctpReceiver != address(0), "CCTP not set");
        require(_to != address(0), "Zero address");
        require(_amount > 0, "Zero amount");
        
        (bool success, ) = cctpReceiver.call(abi.encodeWithSignature("withdrawFunds(address,uint256)", _to, _amount));
        require(success, "Withdrawal failed");
    }
    
    function releasePaymentToTargetChain(string memory _jobId, uint256 _amount, uint32 _targetChainDomain, address _targetRecipient) internal {
        require(cctpTransceiver != address(0), "CCTP not set");
        require(_amount > 0, "Zero amount");
        require(_targetRecipient != address(0), "Zero address");
        
        if (_targetChainDomain == currentChainCCTPDomain) {
            usdtToken.safeTransfer(_targetRecipient, _amount);
            return;
        }
        
        require(chainDomainToCCTPReceiver[_targetChainDomain] != address(0), "Unsupported chain");
        
        usdtToken.approve(cctpTransceiver, _amount);
        
        bytes32 mintRecipient = bytes32(uint256(uint160(_targetRecipient)));
        (bool success, ) = cctpTransceiver.call(
            abi.encodeWithSignature(
                "sendFast(uint256,uint32,bytes32,uint256)", 
                _amount, 
                _targetChainDomain, 
                mintRecipient, 
                1000
            )
        );
        require(success, "Transfer failed");
    }

    // ==================== MESSAGE HANDLERS ====================

    function handleUpdateUserClaimData(address user, uint256 claimedTokens) external {
        require(msg.sender == bridge, "Only bridge");
        
        genesis.updateUserClaimData(user, claimedTokens);
        
        if (address(rewardsContract) != address(0)) {
            rewardsContract.markTokensClaimed(user, claimedTokens);
        }
        
        emit ClaimDataUpdated(user, claimedTokens, 0);
    }

    function incrementGovernanceAction(address user) external {
        require(msg.sender == bridge || authorizedContracts[msg.sender], "Only bridge/authorized");        
        
        genesis.incrementUserGovernanceActions(user);
        
        if (address(rewardsContract) != address(0)) {
            rewardsContract.recordGovernanceAction(user);
        }
        
        uint256 currentBand = address(rewardsContract) != address(0) ? 
            rewardsContract.getCurrentBand() : 0;
        
        uint256 newTotal = genesis.getUserGovernanceActions(user);
        emit GovernanceActionIncremented(user, newTotal, currentBand);
    }

    function _processRewardsForPayment(address jobGiver, string memory jobId, uint256 amount) internal {
        if (address(rewardsContract) == address(0)) return;
        
        IOpenworkGenesis.Job memory job = genesis.getJob(jobId);
        address jobTaker = job.selectedApplicant;
        
        uint256 newPlatformTotal = genesis.totalPlatformPayments();
        
        uint256[] memory tokensAwarded = rewardsContract.processJobPayment(jobGiver, jobTaker, amount, newPlatformTotal);
        
        if (tokensAwarded.length > 0 && tokensAwarded[0] > 0) {
            uint256 currentTokens = genesis.getUserEarnedTokens(jobGiver);
            genesis.setUserTotalOWTokens(jobGiver, currentTokens + tokensAwarded[0]);
            emit TokensEarned(jobGiver, tokensAwarded[0], newPlatformTotal, currentTokens + tokensAwarded[0]);
        }
        
        if (tokensAwarded.length > 1 && tokensAwarded[1] > 0) {
            address jobGiverReferrer = genesis.getUserReferrer(jobGiver);
            if (jobGiverReferrer != address(0)) {
                uint256 currentTokens = genesis.getUserEarnedTokens(jobGiverReferrer);
                genesis.setUserTotalOWTokens(jobGiverReferrer, currentTokens + tokensAwarded[1]);
                emit TokensEarned(jobGiverReferrer, tokensAwarded[1], newPlatformTotal, currentTokens + tokensAwarded[1]);
            }
        }
        
        if (tokensAwarded.length > 2 && tokensAwarded[2] > 0) {
            address jobTakerReferrer = genesis.getUserReferrer(jobTaker);
            if (jobTakerReferrer != address(0)) {
                uint256 currentTokens = genesis.getUserEarnedTokens(jobTakerReferrer);
                genesis.setUserTotalOWTokens(jobTakerReferrer, currentTokens + tokensAwarded[2]);
                emit TokensEarned(jobTakerReferrer, tokensAwarded[2], newPlatformTotal, currentTokens + tokensAwarded[2]);
            }
        }
    }

    function syncVotingPower(bytes calldata _options) external payable {
        require(bridge != address(0), "Bridge not set");
        
        uint256 totalEarnedTokens = address(rewardsContract) != address(0) ? 
            rewardsContract.getUserTotalTokensEarned(msg.sender) : 0;
        
        require(totalEarnedTokens > 0, "No tokens");
        
        INativeBridge(bridge).sendSyncVotingPower{value: msg.value}(msg.sender, totalEarnedTokens, _options);
        
        emit RewardsDataSynced(msg.sender, 2, totalEarnedTokens, 0);
    }

    // ==================== REWARDS VIEW FUNCTIONS ====================
    
    function getUserEarnedTokens(address user) external view returns (uint256) {
        if (address(rewardsContract) != address(0)) {
            return rewardsContract.getUserTotalTokensEarned(user);
        }
        return genesis.getUserEarnedTokens(user);
    }

    function getUserRewardInfo(address user) external view returns (uint256 totalTokens, uint256 governanceActions) {
        if (address(rewardsContract) != address(0)) {
            totalTokens = rewardsContract.getUserTotalTokensEarned(user);
            governanceActions = rewardsContract.getUserTotalGovernanceActions(user);
        } else {
            return genesis.getUserRewardInfo(user);
        }
    }

    function getUserGovernanceActions(address user) external view returns (uint256) {
        if (address(rewardsContract) != address(0)) {
            return rewardsContract.getUserTotalGovernanceActions(user);
        }
        return genesis.getUserGovernanceActions(user);
    }

    function getUserGovernanceActionsInBand(address user, uint256 band) external view returns (uint256) {
        if (address(rewardsContract) != address(0)) {
            return rewardsContract.getUserGovernanceActionsInBand(user, band);
        }
        return genesis.getUserGovernanceActionsInBand(user, band);
    }

    function calculateTokensForAmount(address /* user */, uint256 additionalAmount) external view returns (uint256) {
        if (address(rewardsContract) != address(0)) {
            uint256 currentPlatformTotal = genesis.totalPlatformPayments();
            uint256 newPlatformTotal = currentPlatformTotal + additionalAmount;
            return rewardsContract.calculateTokensForRange(currentPlatformTotal, newPlatformTotal);
        }
        return 0;
    }

    function getUserTotalClaimableTokens(address user) external view returns (uint256) {
        if (address(rewardsContract) != address(0)) {
            return rewardsContract.getUserTotalClaimableTokens(user);
        }
        return 0;
    }

    function getCurrentBand() external view returns (uint256) {
        if (address(rewardsContract) != address(0)) {
            return rewardsContract.getCurrentBand();
        }
        return 0;
    }

    function getPlatformBandInfo() external view returns (uint256 currentBand, uint256 currentTotal, uint256 bandMinAmount, uint256 bandMaxAmount, uint256 governanceRewardRate) {
        if (address(rewardsContract) != address(0)) {
            return rewardsContract.getPlatformBandInfo();
        }
        return (0, genesis.totalPlatformPayments(), 0, 0, 0);
    }

    // ==================== JOB MANAGEMENT FUNCTIONS ====================
    
    function createProfile(address _user, string memory _ipfsHash, address _referrerAddress) external {
        require(!genesis.hasProfile(_user), "Profile exists");

        allProfileUsers.push(_user);
        profileCount++;
        
        genesis.setProfile(_user, _ipfsHash, _referrerAddress);
        emit ProfileCreated(_user, _ipfsHash, _referrerAddress);
    }
    
    function getProfile(address _user) public view returns (IOpenworkGenesis.Profile memory) {
        return genesis.getProfile(_user);
    }
    
    function postJob(string memory _jobId, address _jobGiver, string memory _jobDetailHash, string[] memory _descriptions, uint256[] memory _amounts, uint32 _posterChainDomain, address _posterAddress) external {
        require(!genesis.jobExists(_jobId), "Job exists");
        require(_descriptions.length == _amounts.length, "Length mismatch");
        
        genesis.setJob(_jobId, _jobGiver, _jobDetailHash, _descriptions, _amounts);
        emit JobPosted(_jobId, _jobGiver, _jobDetailHash);
        emit JobStatusChanged(_jobId, JobStatus.Open);
    }
    
    function getJob(string memory _jobId) public view returns (IOpenworkGenesis.Job memory) {
        return genesis.getJob(_jobId);
    }
    
    function _convertMilestones(IOpenworkGenesis.MilestonePayment[] memory genesisMilestones) private pure returns (IOpenworkGenesis.MilestonePayment[] memory) {
        IOpenworkGenesis.MilestonePayment[] memory milestones = new IOpenworkGenesis.MilestonePayment[](genesisMilestones.length);
        for (uint i = 0; i < genesisMilestones.length; i++) {
            milestones[i] = IOpenworkGenesis.MilestonePayment({
                descriptionHash: genesisMilestones[i].descriptionHash,
                amount: genesisMilestones[i].amount
            });
        }
        return milestones;
    }
    
    function applyToJob(address _applicant, string memory _jobId, string memory _applicationHash, string[] memory _descriptions, uint256[] memory _amounts, uint32 _preferredPaymentChainDomain, address _preferredPaymentAddress) external {
        require(_descriptions.length == _amounts.length, "Length mismatch");
        
        IOpenworkGenesis.Job memory job = genesis.getJob(_jobId);
        for (uint i = 0; i < job.applicants.length; i++) {
            require(job.applicants[i] != _applicant, "Already applied");
        }
        
        genesis.addJobApplicant(_jobId, _applicant);
        uint256 applicationId = genesis.getJobApplicationCount(_jobId) + 1;
        genesis.setJobApplication(_jobId, applicationId, _applicant, _applicationHash, _descriptions, _amounts, _preferredPaymentChainDomain, _preferredPaymentAddress);
        emit JobApplication(_jobId, applicationId, _applicant, _applicationHash);
    }
    
    function startJob(address /* _jobGiver */, string memory _jobId, uint256 _applicationId, bool _useApplicantMilestones, uint32 _posterChainDomain, address _posterAddress, uint32 _paymentTargetChainDomain, address _paymentTargetAddress) external {
        IOpenworkGenesis.Application memory application = genesis.getJobApplication(_jobId, _applicationId);
        IOpenworkGenesis.Job memory job = genesis.getJob(_jobId);
        
        genesis.setJobSelectedApplicant(_jobId, application.applicant, _applicationId);
        genesis.updateJobStatus(_jobId, IOpenworkGenesis.JobStatus.InProgress);
        genesis.setJobCurrentMilestone(_jobId, 1);
        
        genesis.setJobPaymentTarget(_jobId, _paymentTargetChainDomain, _paymentTargetAddress, currentChainCCTPDomain);
        
        if (_useApplicantMilestones) {
            for (uint i = 0; i < application.proposedMilestones.length; i++) {
                genesis.addJobFinalMilestone(_jobId, application.proposedMilestones[i].descriptionHash, application.proposedMilestones[i].amount);
            }
        } else {
            for (uint i = 0; i < job.milestonePayments.length; i++) {
                genesis.addJobFinalMilestone(_jobId, job.milestonePayments[i].descriptionHash, job.milestonePayments[i].amount);
            }
        }
        
        emit JobStarted(_jobId, _applicationId, application.applicant, _useApplicantMilestones);
        emit JobStatusChanged(_jobId, JobStatus.InProgress);
    }
    
    function getApplication(string memory _jobId, uint256 _applicationId) public view returns (IOpenworkGenesis.Application memory) {
        require(genesis.getJobApplicationCount(_jobId) >= _applicationId, "Application not found");
        return genesis.getJobApplication(_jobId, _applicationId);
    }
    
    function submitWork(address _applicant, string memory _jobId, string memory _submissionHash) external {
        genesis.addWorkSubmission(_jobId, _submissionHash);
        IOpenworkGenesis.Job memory job = genesis.getJob(_jobId);
        emit WorkSubmitted(_jobId, _applicant, _submissionHash, job.currentMilestone);
    }
    
    function releasePayment(address _jobGiver, string memory _jobId, uint256 _amount, address _applierAddress, uint32 _applierChainDomain, uint256 _milestone, uint32 _posterChainDomain, address _posterAddress, uint32 _paymentTargetChainDomain, address _paymentTargetAddress) external {
        require(msg.sender == bridge, "Only bridge");
        
        IOpenworkGenesis.Job memory job = genesis.getJob(_jobId);
        require(job.selectedApplicant != address(0), "No applicant");
        
        uint32 targetDomain = _paymentTargetChainDomain != 0 ? _paymentTargetChainDomain : _applierChainDomain;
        address targetAddress = _paymentTargetAddress != address(0) ? _paymentTargetAddress : _applierAddress;
        
        releasePaymentToTargetChain(_jobId, _amount, targetDomain, targetAddress);
        
        genesis.updateJobTotalPaid(_jobId, _amount);

        _processRewardsForPayment(_jobGiver, _jobId, _amount);
        
        if (job.currentMilestone == job.finalMilestones.length) {
            genesis.updateJobStatus(_jobId, IOpenworkGenesis.JobStatus.Completed);
            emit JobStatusChanged(_jobId, JobStatus.Completed);
        }
        
        emit PaymentReleased(_jobId, _jobGiver, job.selectedApplicant, _amount, job.currentMilestone);
    }
    
    function lockNextMilestone(address /* _caller */, string memory _jobId, uint256 _lockedAmount) external {
        IOpenworkGenesis.Job memory job = genesis.getJob(_jobId);
        require(job.currentMilestone < job.finalMilestones.length, "All complete");
        
        genesis.setJobCurrentMilestone(_jobId, job.currentMilestone + 1);
        emit MilestoneLocked(_jobId, job.currentMilestone + 1, _lockedAmount);
    }
    
    function releasePaymentAndLockNext(address _jobGiver, string memory _jobId, uint256 _releasedAmount, uint256 _lockedAmount) external {
        require(msg.sender == bridge, "Only bridge");
        
        IOpenworkGenesis.Job memory job = genesis.getJob(_jobId);
        require(job.selectedApplicant != address(0), "No applicant");
        
        withdrawFunds(job.selectedApplicant, _releasedAmount);
        
        genesis.updateJobTotalPaid(_jobId, _releasedAmount);

        _processRewardsForPayment(_jobGiver, _jobId, _releasedAmount);
        
        genesis.setJobCurrentMilestone(_jobId, job.currentMilestone + 1);
        
        job = genesis.getJob(_jobId);
        if (job.currentMilestone > job.finalMilestones.length) {
            genesis.updateJobStatus(_jobId, IOpenworkGenesis.JobStatus.Completed);
            emit JobStatusChanged(_jobId, JobStatus.Completed);
        }
        
        emit PaymentReleasedAndNextMilestoneLocked(_jobId, _releasedAmount, _lockedAmount, job.currentMilestone);
    }
    
    function rate(address _rater, string memory _jobId, address _userToRate, uint256 _rating) external {
        IOpenworkGenesis.Job memory job = genesis.getJob(_jobId);
        bool isAuthorized = false;
        
        if (_rater == job.jobGiver && _userToRate == job.selectedApplicant) {
            isAuthorized = true;
        } else if (_rater == job.selectedApplicant && _userToRate == job.jobGiver) {
            isAuthorized = true;
        }
        
        require(isAuthorized, "Not authorized");
        
        genesis.setJobRating(_jobId, _userToRate, _rating);
        emit UserRated(_jobId, _rater, _userToRate, _rating);
    }

    function addPortfolio(address _user, string memory _portfolioHash) external {
        genesis.addPortfolio(_user, _portfolioHash);
        emit PortfolioAdded(_user, _portfolioHash);
    }

    function syncRewardsData(bytes calldata _options) external payable {
        require(bridge != address(0), "Bridge not set");
        
        uint256 claimableAmount = address(rewardsContract) != address(0) ? 
            rewardsContract.getUserTotalClaimableTokens(msg.sender) : 0;
        
        require(claimableAmount > 0, "No tokens");
        
        INativeBridge(bridge).sendSyncRewardsData{value: msg.value}(msg.sender, claimableAmount, _options);
        
        emit RewardsDataSynced(msg.sender, 1, claimableAmount, 0);
    }
    
    function getRating(address _user) public view returns (uint256) {
        uint256[] memory ratings = genesis.getUserRatings(_user);
        if (ratings.length == 0) {
            return 0;
        }
        
        uint256 totalRating = 0;
        for (uint i = 0; i < ratings.length; i++) {
            totalRating += ratings[i];
        }
        
        return totalRating / ratings.length;
    }
    
    // ==================== VIEW FUNCTIONS ====================
    
    function getProfileCount() external view returns (uint256) {
        return profileCount;
    }

    function getProfileByIndex(uint256 _index) external view returns (IOpenworkGenesis.Profile memory) {
        require(_index < profileCount, "Index out of bounds");
        address userAddress = allProfileUsers[_index];
        return genesis.getProfile(userAddress);
    }

    function getAllProfileUsers() external view returns (address[] memory) {
        return allProfileUsers;
    }
    
    function getJobCount() external view returns (uint256) {
        return genesis.getJobCount();
    }
    
    function getAllJobIds() external view returns (string[] memory) {
        return genesis.getAllJobIds();
    }
    
    function getJobsByPoster(address _poster) external view returns (string[] memory) {
        return genesis.getJobsByPoster(_poster);
    }
    
    function getJobApplicationCount(string memory _jobId) external view returns (uint256) {
        return genesis.getJobApplicationCount(_jobId);
    }
    
    function isJobOpen(string memory _jobId) external view returns (bool) {
        IOpenworkGenesis.Job memory job = genesis.getJob(_jobId);
        return job.status == IOpenworkGenesis.JobStatus.Open;
    }
    
    function getJobStatus(string memory _jobId) external view returns (JobStatus) {
        IOpenworkGenesis.Job memory job = genesis.getJob(_jobId);
        return JobStatus(uint8(job.status));
    }
    
    function jobExists(string memory _jobId) external view returns (bool) {
        return genesis.jobExists(_jobId);
    }

    function getUserReferrer(address user) external view returns (address) {
        return genesis.getUserReferrer(user);
    }
    
    function getTotalPlatformPayments() external view returns (uint256) {
        return genesis.totalPlatformPayments();
    }
    
    function emergencyWithdrawUSDT() external onlyOwner {
        uint256 balance = usdtToken.balanceOf(address(this));
        require(balance > 0, "No balance");
        usdtToken.safeTransfer(owner(), balance);
    }
    
    function setUSDTToken(address _newToken) external onlyOwner {
        require(_newToken != address(0), "Zero address");
        usdtToken = IERC20(_newToken);
    }
}